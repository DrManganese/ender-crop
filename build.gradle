buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "4.1.+", changing: true
        classpath group: "gradle.plugin.com.matthewprenger", name : "CurseGradle", version: "1.4.0"
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.matthewprenger.cursegradle"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

version = minecraft_version + "-" + mod_version
group = "io.github.drmanganese.endercrop"
archivesBaseName = "endercrop"

minecraft {
    mappings channel: mcp_channel, version: mcp_mappings_version
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.console.level", "debug"
            mods {
                endercrop {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run-server")
            property "forge.logging.console.level", "debug"
            mods {
                endercrop {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven { url "https://maven.tterrag.com" }
    maven { url "http://dvs1.progwml6.com/files/maven" }
}

dependencies {
    minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"
    compile fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.0.7-13") {transitive = false}
    runtime fg.deobf("slimeknights.mantle:Mantle:1.16.5-1.6.92")
    compile fg.deobf("slimeknights.tconstruct:TConstruct:1.16.5-3.0.2.100") {transitive = false}
}

jar {
    manifest {
        attributes([
                "FMLAT": "accesstransformer.cfg",
                "Specification-Title": "endercrop",
                "Specification-Vendor": "DrManganese",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": getArchiveVersion(),
                "Implementation-Vendor" :"DrManganese",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from "LICENSE"
}

curseforge {
    apiKey = curseforgeToken
    project {
        id = "242269"
        changelogType = "markdown"
        changelog = file("CHANGELOG.md")
        releaseType = "release"
        mainArtifact jar
    }
}
